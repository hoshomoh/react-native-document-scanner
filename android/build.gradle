buildscript {
  ext.safeAppExt = { prop, fallback ->
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
  }
  repositories {
    google()
    mavenCentral()
  }
  dependencies {
    classpath("com.android.tools.build:gradle:${project.findProperty('DocumentScanner_gradlePluginVersion') ?: '8.2.1'}")
    classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${project.findProperty('DocumentScanner_kotlinVersion') ?: '1.9.22'}")
  }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
  namespace project.findProperty('DocumentScanner_namespace') ?: "com.documentscanner"
  compileSdkVersion safeAppExt('compileSdkVersion', project.findProperty('DocumentScanner_compileSdkVersion')?.toInteger() ?: 34)

  defaultConfig {
    minSdkVersion safeAppExt('minSdkVersion', project.findProperty('DocumentScanner_minSdkVersion')?.toInteger() ?: 24)
    targetSdkVersion safeAppExt('targetSdkVersion', project.findProperty('DocumentScanner_targetSdkVersion')?.toInteger() ?: 34)
    versionCode 1
    versionName "1.0"
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable 'GradleCompatible'
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

repositories {
  mavenLocal()
  maven {
    // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
    url("$rootDir/../node_modules/react-native/android")
  }
  google()
  mavenCentral()
}

dependencies {
  implementation "com.facebook.react:react-android"
  implementation "com.google.android.gms:play-services-mlkit-document-scanner:${project.findProperty('DocumentScanner_mlkitScannerVersion') ?: '16.0.0'}"
  implementation "com.google.android.gms:play-services-mlkit-text-recognition:${project.findProperty('DocumentScanner_mlkitTextRecognitionVersion') ?: '19.0.0'}"
  implementation "androidx.exifinterface:exifinterface:${project.findProperty('DocumentScanner_exifInterfaceVersion') ?: '1.3.7'}"
}

