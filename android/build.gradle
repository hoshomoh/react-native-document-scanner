buildscript {
  // Helper function to get version from app's rootProject or fall back to library's gradle.properties
  ext.getExtOrDefault = { name ->
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties['DocumentScanner_' + name]
  }

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:${getExtOrDefault('gradlePluginVersion')}"
    // noinspection DifferentKotlinGradleVersion
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${getExtOrDefault('kotlinVersion')}"
  }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.facebook.react'

// Helper function that returns an Integer for SDK version properties
def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["DocumentScanner_" + name]).toInteger()
}

android {
  namespace "com.documentscanner"

  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
  }

  buildFeatures {
    buildConfig true
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

repositories {
  google()
  mavenCentral()
}

def kotlin_version = getExtOrDefault("kotlinVersion")

dependencies {
  implementation "com.facebook.react:react-android"
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  implementation "com.google.android.gms:play-services-mlkit-document-scanner:${getExtOrDefault('mlkitScannerVersion')}"
  implementation "com.google.android.gms:play-services-mlkit-text-recognition:${getExtOrDefault('mlkitTextRecognitionVersion')}"
  implementation "androidx.exifinterface:exifinterface:${getExtOrDefault('exifInterfaceVersion')}"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:${getExtOrDefault('kotlinCoroutinesPlayServicesVersion')}"
}
